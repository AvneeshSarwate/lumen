;; -*- mode: lisp -*-

;; infrastructure

(set passed 0)
(set failed 0)
(set tests ())

(mac test (x msg)
  `(if (not ,x)
       (do (set failed (+ failed 1))
	   (return ,msg))
     (set passed (+ passed 1))))

(def equal? (a b)
  (if (atom? a)
      (= a b)
    (= (to-string a) (to-string b))))

(mac t= (a b)
  `(test (equal? ,a ,b)
	 (cat " failed: expected " (to-string ,a) ", was " (to-string ,b))))

(mac deftest (name _ body...)
  `(push tests (list ',name (fn () ,@body))))

(set run-tests
  (fn ()
    (across (tests test)
      (let (name (at test 0)
	    f (at test 1)
	    result (f))
	(if (string? result)
	    (prn  " " name result))))
    (prn passed " passed, " failed " failed")))

;; basic

(deftest reader ()
  (t= 17 (read-from-string "17"))
  (t= 0.015 (read-from-string "1.5e-2"))
  (t= true (read-from-string "true"))
  (t= (not true) (read-from-string "false"))
  (t= 'hi (read-from-string "hi"))
  (t= '"hi" (read-from-string "\"hi\""))
  (t= '(1 2) (read-from-string "(1 2)"))
  (t= '(1 (a)) (read-from-string "(1 (a))"))
  (t= '(quote a) (read-from-string "'a"))
  (t= '(quasiquote a) (read-from-string "`a"))
  (t= '(quasiquote (unquote a)) (read-from-string "`,a"))
  (t= '(quasiquote (unquote-splicing a)) (read-from-string "`,@a")))

(deftest boolean ()
  (t= true (or true false))
  (t= false (or false false))
  (t= true (not false))
  (t= true (and true true))
  (t= false (and true false))
  (t= false (and true true false)))

(deftest numeric ()
  (t= 4 (+ 2 2))
  (t= 18 18.00)
  (t= 4 (- 7 3))
  (t= 5.0 (/ 10 2))
  (t= 6 (* 2 3.00))
  (t= true (> 2.01 2))
  (t= true (>= 5.0 5.0))
  (t= false (< 2 2))
  (t= true (<= 2 2))
  (t= -7 (- 7)))

(deftest string ()
  (t= 3 (length "foo"))
  (t= 3 (length "\"a\""))
  (t= 'a "a")
  (t= "a" (char "bar" 1)))

(deftest quote ()
  (t= 7 (quote 7))
  (t= true (quote true))
  (t= false (quote false))
  (t= (quote a) 'a)
  (t= (quote (quote a)) ''a)
  (t= "\"a\"" '"a")
  (t= '(quote "a") ''"a")
  (t= (quote unquote) 'unquote)
  (t= (quote (unquote)) '(unquote))
  (t= (quote (unquote a)) '(unquote a)))

(deftest list ()
  (t= '() (list))
  (t= () (list))
  (t= '(a) (list 'a))
  (t= '(a) (quote (a)))
  (t= '(()) (list (list)))
  (t= 0 (length (list)))
  (t= 2 (length (list 1 2))))

(deftest quasiquote ()
  (t= (quote a) (quasiquote a))
  (t= 'a `a)
  (t= '() `())
  (t= () `())
  (t= 2 `,2)
  (let (a 42)
    (t= 42 `,a)
    (t= 42 (quasiquote (unquote a)))
    (t= '(quasiquote (unquote a)) ``,a)
    (t= '(quasiquote (unquote 42)) ``,,a)
    (t= '(quasiquote (quasiquote (unquote (unquote a)))) ```,,a)
    (t= '(quasiquote (quasiquote (unquote (unquote 42)))) ```,,,a)
    (t= '(a (quasiquote (b (unquote c)))) `(a `(b ,c)))
    (t= '(a (quasiquote (b (unquote 42)))) `(a `(b ,,a)))
    (let (b 'c)
      (t= '(quote c) `',b)
      (t= '(42) `(,a))
      (t= '((42)) `((,a)))
      (t= '(41 (42)) `(41 (,a)))))
  (let (c '(1 2 3))
    (t= '((1 2 3)) `(,c))
    (t= '(1 2 3) `(,@c))
    (t= '(0 1 2 3) `(0 ,@c))
    (t= '(0 1 2 3 4) `(0 ,@c 4))
    (t= '(0 (1 2 3) 4) `(0 (,@c) 4))
    (t= '(1 2 3 1 2 3) `(,@c ,@c))
    (t= '((1 2 3) 1 2 3) `((,@c) ,@c)))
  (let (a 42)
    (t= '(quasiquote ((unquote-splicing (list a)))) ``(,@(list a)))
    (t= '(quasiquote ((unquote-splicing (list 42)))) ``(,@(list ,a)))))

(deftest calls ()
  (let (f (fn () 42)
	l (list f)
	t (table f f))
    (t= 42 (f))
    (t= 42 ((at l 0)))
    (t= 42 (t.f))
    (t= 42 ((dot t f)))))

(deftest quasiexpand ()
  (t= 'a (macroexpand 'a))
  (t= '(17) (macroexpand '(17)))
  (t= '(1 z) (macroexpand '(1 z)))
  (t= '(list '1 'z) (macroexpand '`(1 z)))
  (t= '(list 1 z) (macroexpand '`(,1 ,z)))
  (t= 'z (macroexpand '`(,@z)))
  (t= '(join (list 1) z) (macroexpand '`(,1 ,@z)))
  (t= '(join (list 1) (join x y)) (macroexpand '`(,1 ,@x ,@y)))
  (t= '(join (list 1) (join z (list 2))) (macroexpand '`(,1 ,@z ,2)))
  (t= '(join (list 1) (join z (list 'a))) (macroexpand '`(,1 ,@z a)))
  (t= '(quote x) (macroexpand '`x))
  (t= '(list 'quasiquote 'x) (macroexpand '``x))
  (t= '(list 'quasiquote (list 'quasiquote 'x)) (macroexpand '```x))
  (t= 'x (macroexpand '`,x))
  (t= '(list 'quote x) (macroexpand '`',x))
  (t= '(list 'quasiquote (list 'x)) (macroexpand '``(x)))
  (t= '(list 'quasiquote (list 'unquote 'a)) (macroexpand '``,a))
  (t= '(list 'quasiquote (list (list 'unquote 'x))) (macroexpand '``(,x))))

;; special forms

(deftest local ()
  (local a 42)
  (t= 42 a))

(deftest set ()
  (let (a 42)
    (set a 'bar)
    (t= 'bar a)))

(deftest do ()
  (let (a 17)
    (do (set a 10)
	(t= 10 a))
    ;; do cannot introduce a new scope
    (do (local a 7)
	(t= 7 a))
    (t= 7 a)))

(deftest if ()
  (if true
      (t= true true)
    (t= true false)))

(deftest while ()
  (let (i 0)
    (while (< i 10)
      (set i (+ i 1)))
    (t= 10 i)))

(deftest table ()
  (t= (table a 10 b 20) (table a 10 b 20))
  (t= 10 (get (table a 10) 'a)))

(deftest get-set ()
  (let (t (table))
    (set (get t 'foo) 'bar)
    (t= 'bar (get t 'foo))))

(deftest dot ()
  (let (t (table a 10))
    (t= 10 t.a)
    (t= 10 (dot t a))))

(deftest each ()
  (let (a "" b 0)
    (each ((table a 10 b 20 c 30) k v)
      (cat! a k)
      (set b (+ b v)))
    (t= 3 (length a))
    (t= 60 b)))

(deftest fn ()
  (let (f (fn (n) (+ n 10)))
    (t= 20 (f 10))
    (t= 30 (f 20))
    (t= 40 ((fn (n) (+ n 10)) 30))))

;; macros

(deftest let ()
  (let (a 10)
    (t= 10 a))
  (let (a 11
	b 12)
    (t= 11 a)
    (t= 12 b))
  (let (a 1)
    (t= 1 a)
    (let (a 2)
      (t= 2 a))
    (t= 1 a))
  ((fn (zz)
     (t= 20 zz)
     (let (zz 21)
       (t= 21 zz))
     (t= 20 zz))
   20))

(deftest letmac ()
  (letmac ((a () 17)
	   (b (a) `(+ ,a 10)))
    (t= 17 (a))
    (t= 42 (b 32))
    (letmac ((a () 1))
      (t= 1 (a)))
    (t= 17 (a)))
  (letmac ((a () 18))
    (let (b (fn () 20))
      (t= 18 (a))
      (t= 20 (b)))))

(deftest letsym ()
  (letsym ((a 17)
	   (b (+ 10 7)))
    (t= 17 a)
    (t= 17 b)
    (letsym ((a 1))
      (t= 1 a))
    (t= 17 a))
  (letsym ((a 18))
    (let (b 20)
      (t= 18 a)
      (t= 20 b))))

(deftest macros-and-symbol-macros ()
  (letsym ((a 1))
    (letmac ((a () 2))
      (t= 2 (a))))
  (letmac ((a () 2))
    (letsym ((a 1))
      (t= 1 a))))

(deftest macros-and-let ()
  (let (a 10)
    (t= a 10)
    (letmac ((a () 12))
      (t= 12 (a)))
    (t= a 10))
  (let (b 20)
    (t= b 20)
    (letsym ((b 22))
      (t= 22 b))
    (t= b 20))
  (letmac ((c () 30))
    (t= 30 (c))
    (let (c 32)
      (t= 32 c))
    (t= 30 (c)))
  (letsym ((d 40))
    (t= 40 d)
    (let (d 42)
      (t= 42 d))
    (t= 40 d)))

;; expressions

(deftest if-expr ()
  (t= 10 (if true 10 20)))

(deftest set-expr ()
  (let (a 5)
    (t= nil (set a 7))
    (t= 10 (do (set a 10) a))))

(deftest local-expr ()
  (let (a 5)
    (t= 10 (do (local a 10) a))))

(deftest while-expr ()
  (let (i 0)
    (t= 10 (do (while (< i 10) (set i (+ i 1))) i))))

(deftest each-expr ()
  (let (i 0 t (table a 10 b 20))
    (t= 2 (do (each (t _ _) (set i (+ i 1))) i))))

;; library

(deftest push ()
  (let (l ())
    (push l 'a)
    (push l 'b)
    (push l 'c)
    (t= '(a b c) l)))

(deftest pop ()
  (let (l '(a b c))
    (t= 'c (pop l))
    (t= 'b (pop l))
    (t= 'a (pop l))
    (t= nil (pop l))))

(deftest last ()
  (t= 3 (last '(1 2 3)))
  (t= nil (last ()))
  (t= 'c (last '(a b c))))

(deftest join ()
  (t= '(1 2 3) (join '(1 2) '(3)))
  (t= '(1 2) (join () '(1 2)))
  (t= () (join () ())))

(deftest map ()
  (t= '(1) (map (fn (x) x) '(1)))
  (t= '(2 3 4) (map (fn (x) (+ x 1)) '(1 2 3))))

(deftest sub ()
  (t= '(b c) (sub '(a b c) 1))
  (t= '(b c) (sub '(a b c d) 1 3))
  (t= "uux" (sub "quux" 1))
  (t= "uu" (sub "quux" 1 3))
  (t= "" (sub "quux" 5)))

(deftest search ()
  (t= 0 (search "abc" "a"))
  (t= 2 (search "abcd" "cd"))
  (t= nil (search "abc" "z")))

(deftest split ()
  (t= (list "a") (split "a" ","))
  (t= (list "a" "") (split "a," ","))
  (t= (list "a" "b") (split "a,b" ",")))

(deftest reduce ()
  (t= 6 (reduce (fn (a b) (+ a b)) '(1 2 3)))
  (t= '(1 (2 3))
	      (reduce
	       (fn (a b) (list a b))
	       '(1 2 3)))
  (t= '(1 2 3 4 5)
	      (reduce
	       (fn (a b) (join a b))
	       '((1) (2 3) (4 5)))))

(deftest list* ()
  (t= () (list*))
  (t= '(2 3) (list* '(2 3)))
  (t= '(1 2 3) (list* 1 '(2 3)))
  (let (a 17 b '(5))    
    (t= '(5) (list* b))
    (t= '(17 5) (list* a b))
    (t= '((5) 5) (list* b b))))

(deftest type ()
  (t= true (string? "abc"))
  (t= false (string? 17))
  (t= false (string? '(a)))
  (t= false (string? true))
  (t= false (string? (table)))
  (t= false (number? "abc"))
  (t= true (number? 17))
  (t= false (number? '(a)))
  (t= false (number? true))
  (t= false (number? (table)))
  (t= false (boolean? "abc"))
  (t= false (boolean? 17))
  (t= false (boolean? '(a)))
  (t= true (boolean? true))
  (t= false (boolean? (table)))
  (t= false (function? 17))
  (t= false (function? "foo"))
  (t= false (function? '(a)))
  (t= false (function? (table)))
  (t= true (function? (fn () 17)))
  (t= false (list? "abc"))
  (t= false (list? 17))
  (t= true (list? '(a)))
  (t= false (list? true))
  (t= false (list? (table)))
  (t= false (table? "abc"))
  (t= false (table? 17))
  (t= false (table? '(a)))
  (t= false (table? true))
  (t= true (table? (table))))

(deftest apply ()
  (t= 4 (apply (fn (a b) (+ a b)) '(2 2)))
  (t= '(2 2) (apply (fn (a...) a) '(2 2))))

(deftest eval ()
  (let (f (fn (x) (eval (compile (macroexpand x)))))
    (t= 4 (f '(+ 2 2)))
    (t= 5 (f '(let (a 3) (+ 2 a))))))

(deftest bind ()
  (bind (a b c) '(1 2 3))
  (t= 1 a)
  (t= 2 b)
  (t= 3 c)
  (bind (_ b) '(1 2))
  (t= 2 b)
  (bind (w (x (y) z)) '(1 (2 (3) 4)))
  (t= 1 w)
  (t= 2 x)
  (t= 3 y)
  (t= 4 z)
  (bind (a b c...) '(1 2 3 4))
  (t= '(3 4) c)
  (bind (w (x y...) z...) '(1 (2 3 4) 5 6 7))
  (t= '(3 4) y)
  (t= '(5 6 7) z))

(deftest parameters ()
  (let (f (fn (a (b c)) (list a b c)))
    (t= '(1 2 3) (f 1 '(2 3)))
    (set f (fn (a (b c...) d...) (list a b c d)))
    (t= '(1 2 (3 4) (5 6 7)) (f 1 '(2 3 4) 5 6 7))))
