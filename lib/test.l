;; -*- mode: lisp -*-

(define-module test
  import: (runtime
           utilities
           special
           core
           reader
           compiler))

;; infrastructure

(define passed 0)
(define failed 0)
(define tests ())

(define-macro test (x msg)
  `(if (not ,x)
       (do (set failed (+ failed 1))
	   (return ,msg))
     (inc passed)))

(define equal? (a b)
  (if (atom? a) (= a b)
    (= (string a) (string b))))

(define-macro test= (a b)
  `(test (equal? ,a ,b)
	 (cat "failed: expected " (string ,a) ", was " (string ,b))))

(define-macro define-test (name _ rest: body)
  `(add tests (list ',name (fn () ,@body))))

(define run ()
  (pr (cat (language) ":"))
  (each (test) tests
    (let (name (at test 0)
	  f (at test 1)
	  result (f))
      (when (string? result)
        (pr "  " name result))))
  (pr "  " passed "passed," failed "failed"))

(define-test reader ()
  (test= 17 (read-from-string "17"))
  (test= 0.015 (read-from-string "1.5e-2"))
  (test= true (read-from-string "true"))
  (test= (not true) (read-from-string "false"))
  (test= 'hi (read-from-string "hi"))
  (test= '"hi" (read-from-string "\"hi\""))
  (test= "|hi|" (read-from-string "|hi|"))
  (test= '(1 2) (read-from-string "(1 2)"))
  (test= '(1 (a)) (read-from-string "(1 (a))"))
  (test= '(quote a) (read-from-string "'a"))
  (test= '(quasiquote a) (read-from-string "`a"))
  (test= '(quasiquote (unquote a)) (read-from-string "`,a"))
  (test= '(quasiquote (unquote-splicing a)) (read-from-string "`,@a"))
  (test= 2 (length (read-from-string "(1 2 a: 7)")))
  (test= 7 (get (read-from-string "(1 2 a: 7)") 'a))
  (test= true (get (read-from-string "(:a)") 'a)))

(define-test boolean ()
  (test= true (or true false))
  (test= false (or false false))
  (test= true (or false false true))
  (test= true (not false))
  (test= true (not (and false true)))
  (test= false (not (or false true)))
  (test= true (and true true))
  (test= false (and true false))
  (test= false (and true true false)))

(define-test short ()
  (test= true (or true (error 'bad)))
  (test= false (and false (error 'bad)))
  (let (a true)
    (test= true (or true (do (set a false) false)))
    (test= true a)
    (test= false (and false (do (set a false) true)))
    (test= true a))
  (let (b true)
    (test= true (or (do (set b false) false) (do (set b true) b)))
    (test= true b)
    (test= true (or (do (set b true) b) (do (set b true) b)))
    (test= true b)
    (test= true (and (do (set b false) true) (do (set b true) b)))
    (test= true b)
    (test= false (and (do (set b false) b) (do (set b true) b)))
    (test= false b)))

(define-test numeric ()
  (test= 4 (+ 2 2))
  (test= 18 18.00)
  (test= 4 (- 7 3))
  (test= 5.0 (/ 10 2))
  (test= 6 (* 2 3.00))
  (test= true (> 2.01 2))
  (test= true (>= 5.0 5.0))
  (test= true (> 2.1e3 2000))
  (test= true (< 2e-3 0.0021))
  (test= false (< 2 2))
  (test= true (<= 2 2))
  (test= -7 (- 7)))

(define-test math ()
  (test= 3 (max 1 3))
  (test= 2 (min 2 7))
  (let (n (random))
    (test= true (and (> n 0) (< n 1))))
  (test= 4 (floor 4.78)))

(define-test precedence ()
  (test= -3 (- (+ 1 2)))
  (test= 10 (- 12 (+ 1 1)))
  (test= 11 (- 12 (* 1 1)))
  (test= 10 (+ (/ 4 2) 8)))

(define-test string ()
  (test= 3 (length "foo"))
  (test= 3 (length "\"a\""))
  (test= 'a "a")
  (test= "a" (char "bar" 1)))

(define-test quote ()
  (test= 7 (quote 7))
  (test= true (quote true))
  (test= false (quote false))
  (test= (quote a) 'a)
  (test= (quote (quote a)) ''a)
  (test= "\"a\"" '"a")
  (test= "\"\\n\"" (quote "\n"))
  (test= "\"\\\\\"" (quote "\\"))
  (test= '(quote "a") ''"a")
  (test= "|(|" '|(|)
  (test= (quote unquote) 'unquote)
  (test= (quote (unquote)) '(unquote))
  (test= (quote (unquote a)) '(unquote a)))

(define-test list ()
  (test= '() (list))
  (test= () (list))
  (test= '(a) (list 'a))
  (test= '(a) (quote (a)))
  (test= '(()) (list (list)))
  (test= 0 (length (list)))
  (test= 2 (length (list 1 2)))
  (test= '(1 2 3) (list 1 2 3))
  (test= 17 (get (list foo: 17) 'foo))
  (test= 17 (get (list 1 foo: 17) 'foo))
  (test= true (get (list :foo) 'foo))
  (test= true (get '(:foo) 'foo))
  (test= true (get (hd '((:foo))) 'foo))
  (test= '(:a) (list :a))
  (test= '(b: false) (list b: false))
  (test= '(c: 0) (list c: 0)))

(define-test quasiquote ()
  (test= (quote a) (quasiquote a))
  (test= 'a `a)
  (test= () `())
  (test= 2 `,2)
  (test= nil `(,@nil))
  (let (a 42)
    (test= 42 `,a)
    (test= 42 (quasiquote (unquote a)))
    (test= '(quasiquote (unquote a)) ``,a)
    (test= '(quasiquote (unquote 42)) ``,,a)
    (test= '(quasiquote (quasiquote (unquote (unquote a)))) ```,,a)
    (test= '(quasiquote (quasiquote (unquote (unquote 42)))) ```,,,a)
    (test= '(a (quasiquote (b (unquote c)))) `(a `(b ,c)))
    (test= '(a (quasiquote (b (unquote 42)))) `(a `(b ,,a)))
    (let (b 'c)
      (test= '(quote c) `',b)
      (test= '(42) `(,a))
      (test= '((42)) `((,a)))
      (test= '(41 (42)) `(41 (,a)))))
  (let (c '(1 2 3))
    (test= '((1 2 3)) `(,c))
    (test= '(1 2 3) `(,@c))
    (test= '(0 1 2 3) `(0 ,@c))
    (test= '(0 1 2 3 4) `(0 ,@c 4))
    (test= '(0 (1 2 3) 4) `(0 (,@c) 4))
    (test= '(1 2 3 1 2 3) `(,@c ,@c))
    (test= '((1 2 3) 1 2 3) `((,@c) ,@c)))
  (let (a 42)
    (test= '(quasiquote ((unquote-splicing (list a)))) ``(,@(list a)))
    (test= '(quasiquote ((unquote-splicing (list 42)))) ``(,@(list ,a))))
  (test= true (get `(:foo) 'foo))
  (let (a 17
        b '(1 2)
        c (table a: 10)
        d (list a: 10))
    (test= 17 (get `(foo: ,a) 'foo))
    (test= 2 (length `(foo: ,a ,@b)))
    (test= 17 (get `(foo: ,@a) 'foo))
    (test= '(1 a: 10) `(1 ,@c))
    (test= '(1 a: 10) `(1 ,@d))
    (test= true (get (hd `((:foo))) 'foo))
    (test= true (get (hd `(,(list :foo))) 'foo))
    (test= true (get `(,@(list :foo)) 'foo))
    (test= true (get `(1 2 3 ,@'(:foo)) 'foo)))
  (let-macro ((a keys `(table ,@keys)))
    (test= true (get (a :foo) 'foo))
    (test= 17 (get (a bar: 17) 'bar)))
  (let-macro ((a () `(table baz: (fn () 17))))
    (test= 17 ((get (a) 'baz)))))

(define-test quasiexpand ()
  (test= 'a (macroexpand 'a))
  (test= '(17) (macroexpand '(17)))
  (test= '(1 z) (macroexpand '(1 z)))
  (test= '(%array 1 "z") (macroexpand '`(1 z)))
  (test= '(%array 1 z) (macroexpand '`(,1 ,z)))
  (test= 'z (macroexpand '`(,@z)))
  (test= '(join (%array 1) z) (macroexpand '`(,1 ,@z)))
  (test= '(join (%array 1) (join x y)) (macroexpand '`(,1 ,@x ,@y)))
  (test= '(join (%array 1) (join z (%array 2))) (macroexpand '`(,1 ,@z ,2)))
  (test= '(join (%array 1) (join z (%array "a"))) (macroexpand '`(,1 ,@z a)))
  (test= '"x" (macroexpand '`x))
  (test= '(%array "quasiquote" "x") (macroexpand '``x))
  (test= '(%array "quasiquote" (%array "quasiquote" "x")) (macroexpand '```x))
  (test= 'x (macroexpand '`,x))
  (test= '(%array "quote" x) (macroexpand '`',x))
  (test= '(%array "quasiquote" (%array "x")) (macroexpand '``(x)))
  (test= '(%array "quasiquote" (%array "unquote" "a")) (macroexpand '``,a))
  (test= '(%array "quasiquote" (%array (%array "unquote" "x")))
         (macroexpand '``(,x))))

(define-test calls ()
  (let (f (fn () 42)
	l (list f)
	t (table f: f))
    (test= 42 (f))
    (test= 42 ((at l 0)))
    (test= 42 (t.f))
    (test= 42 ((get t 'f)))
    (test= nil ((fn () (return))))
    (test= 10 ((fn (x) (- x 2)) 12))))

(define-test id ()
  (let (a 10
        b (table x: 20)
        f (fn () 30))
    (test= 10 a)
    (test= 10 |a|)
    (test= 20 |b.x|)
    (test= 30 |f()|)))

(define-test set ()
  (let (a 42)
    (set a 'bar)
    (test= 'bar a)
    (let (x (set a 10))
      (test= x nil)
      (test= a 10))
    (set a false)
    (test= a false)
    (set a)
    (test= a nil)))

(define-test do ()
  (let (a 17)
    (do (set a 10)
	(test= 10 a))
    (test= 10 (do a))
    (let (b (do (set a 2) (+ a 5)))
      (test= a 2)
      (test= b 7))
    (do (set a 10)
        (do (set a 20)
            (test= 20 a)))
    (test= 20 (do (set a 10)
                  (do (set a 20) a)))))

(define-test if ()
  (test= 'a (macroexpand '(if a)))
  (test= '(%if a b) (macroexpand '(if a b)))
  (test= '(%if a b c) (macroexpand '(if a b c)))
  (test= '(%if a b (%if c d)) (macroexpand '(if a b c d)))
  (test= '(%if a b (%if c d e)) (macroexpand '(if a b c d e)))
  (if true
      (test= true true)
    (test= true false))
  (if false (test= true false)
      false (test= false true)
    (test= true true))
  (if false (test= true false)
      false (test= false true)
      false (test= false true)
    (test= true true))
  (if false (test= true false)
      true (test= true true)
      false (test= false true)
    (test= true true))
  (test= 1 (if true 1 2))
  (test= 1 (if (let (a 10) a) 1 2))
  (test= 1 (if true (let (a 1) a) 2))
  (test= 1 (if false 2 (let (a 1) a)))
  (test= 1 (if false 2 true (let (a 1) a)))
  (test= 1 (if false 2 false 3 (let (a 1) a))))

(define-test while ()
  (let (i 0)
    (while (< i 5)
      (if (= i 3) (break) (inc i)))
    (test= 3 i)
    (while (< i 10)
      (inc i))
    (test= 10 i)
    (let (a (while (< i 15) (inc i)))
      (test= nil a)
      (test= 15 i))
    (let (b (while (< i 20)
              (if (= i 19)
                  (break)
                (inc i))))
      (test= nil a)
      (test= 19 i))))

(define-test table ()
  (test= 10 (get (table a: 10) 'a))
  (test= true (get (table :a) 'a)))

(define-test empty ()
  (test= true (empty? ()))
  (test= true (empty? (table)))
  (test= false (empty? '(1)))
  (test= false (empty? '(:a)))
  (test= false (empty? (table :a)))
  (test= false (empty? '(b: false))))

(define-test get-set ()
  (let (t (table))
    (set (get t 'foo) 'bar)
    (test= 'bar (get t 'foo))
    (test= 'bar (get t "foo"))
    (let (k 'foo)
      (test= 'bar (get t k)))
    (test= 'bar (get t (cat "f" "oo")))))

(define-test each ()
  (let (t '(1 2 3 :a b: false) i 0)
    (each (x) t
      (inc i)
      (test= true (number? x)))
    (test= 3 i)
    (each (k v) t
      (inc i)
      (test= true (string? k))
      (test= true (boolean? v)))
    (test= 5 i)
    (each (x) t
      (if (= x 2) (break) (inc i)))
    (test= 6 i)
    (let (a (each (_) t (inc i)))
      (test= a nil)
      (test= 9 i))
    (let (b (each (_ _) t (inc i)))
      (test= b nil)
      (test= 11 i))))

(define-test fn ()
  (let (f (fn (n) (+ n 10)))
    (test= 20 (f 10))
    (test= 30 (f 20))
    (test= 40 ((fn (n) (+ n 10)) 30))
    (test= '(2 3 4) (map (fn (x) (+ x 1)) '(1 2 3)))))

(define-test return ()
  (let (a ((fn () 17)))
    (test= 17 a))
  (let (a ((fn () (if true 10 20))))
    (test= 10 a))
  (let (a ((fn () (while false (blah)))))
    (test= nil a))
  (let (a 11)
    (let (b ((fn () (inc a))))
      (test= nil b)
      (test= 12 a))))

(define-test guard ()
  (test= '(true 42) (guard 42))
  (test= '(false foo) (guard (error "foo")))
  (test= '(false foo) (guard (do (error "foo") (error "baz"))))
  (test= '(true 42) (guard (if true 42 (error "baz"))))
  (test= '(false baz) (guard (if false 42 (error "baz")))))

(define-test let ()
  (let (a 10)
    (test= 10 a))
  (let (a 11
	b 12)
    (test= 11 a)
    (test= 12 b))
  (let (a 1)
    (test= 1 a)
    (let (a 2)
      (test= 2 a))
    (test= 1 a))
  (let (a 1)
    (let (a 2)
      (let (a 3)
        (test= a 3))
      (test= a 2))
    (test= a 1))
  (let (a 20)
    (test= 20 a)
    (let (a (+ a 7))
      (test= 27 a))
    (let (a (+ a 10))
      (test= 30 a))
    (test= 20 a))
  (test= 10 (let (a 10) a))
  (let (a (let (b 12) b))
    (test= 12 a))
  (let (a (let (a 10) a))
    (test= 10 a))
  (let (a (+ (let (a 0)
               (set a 10)
               (+ a 2))
             3))
    (test= a 15))
  ((fn (zz)
     (test= 20 zz)
     (let (zz 21)
       (test= 21 zz))
     (test= 20 zz))
   20))

(define-test destructuring ()
  (let ((a b c) '(1 2 3))
    (test= 1 a)
    (test= 2 b)
    (test= 3 c))
  (let ((_ b) '(1 2))
    (test= 2 b))
  (let ((w (x (y) z)) '(1 (2 (3) 4)))
    (test= 1 w)
    (test= 2 x)
    (test= 3 y)
    (test= 4 z))
  (let ((a b rest: c) '(1 2 3 4))
    (test= '(3 4) c))
  (let ((w (x rest: y) rest: z) '(1 (2 3 4) 5 6 7))
    (test= '(3 4) y)
    (test= '(5 6 7) z))
  (let ((:foo) (table foo: 99))
    (test= 99 foo))
  (let ((:foo) (list foo: 99))
    (test= 99 foo))
  (let ((foo: a) (table foo: 99))
    (test= 99 a))
  (let ((foo: (a b)) (table foo: '(98 99)))
    (test= 98 a)
    (test= 99 b))
  (let ((:foo bar: (:baz))
        (table foo: 42 bar: '(99 :baz)))
    (test= 42 foo)
    (test= true baz))
  (let ((a (b :foo) :bar)
        (list 10 (list 20 foo: 17) bar: '(1 2 3)))
    (test= 10 a)
    (test= 20 b)
    (test= 17 foo)
    (test= '(1 2 3) bar)))

(define-test let-macro ()
  (let-macro ((a () 17)
              (b (a) `(+ ,a 10)))
    (test= 17 (a))
    (test= 42 (b 32))
    (let-macro ((a () 1))
      (test= 1 (a)))
    (test= 17 (a)))
  (let-macro ((a () 18))
    (let (b (fn () 20))
      (test= 18 (a))
      (test= 20 (b))))
  (let-macro ((a (x)
                (let (x 10)
                  (set x 20))
                `(+ ,x 1)))
    (test= 2 (a 1))))

(define-test let-symbol ()
  (let-symbol (a 17
               b (+ 10 7))
    (test= 17 a)
    (test= 17 b)
    (let-symbol (a 1)
      (test= 1 a))
    (test= 17 a))
  (let-symbol (a 18)
    (let (b 20)
      (test= 18 a)
      (test= 20 b))))

(define-test define-symbol ()
  (define-symbol zzz 42)
  (test= zzz 42))

(define-test macros-and-symbols ()
  (let-symbol (a 1)
    (let-macro ((a () 2))
      (test= 2 (a)))
    (test= 1 a))
  (let-macro ((a () 2))
    (let-symbol (a 1)
      (test= 1 a))
    (test= 2 (a))))

(define-test macros-and-let ()
  (let (a 10)
    (test= a 10)
    (let-macro ((a () 12))
      (test= 12 (a)))
    (test= a 10))
  (let (b 20)
    (test= b 20)
    (let-symbol (b 22)
      (test= 22 b))
    (test= b 20))
  (let-macro ((c () 30))
    (test= 30 (c))
    (let (c 32)
      (test= 32 c))
    (test= 30 (c)))
  (let-symbol (d 40)
    (test= 40 d)
    (let (d 42)
      (test= 42 d))
    (test= 40 d)))

(define-test add ()
  (let (l ())
    (add l 'a)
    (add l 'b)
    (add l 'c)
    (test= '(a b c) l)
    (test= nil (add () 'a))))

(define-test drop ()
  (let (l '(a b c))
    (test= 'c (drop l))
    (test= 'b (drop l))
    (test= 'a (drop l))
    (test= nil (drop l))))

(define-test last ()
  (test= 3 (last '(1 2 3)))
  (test= nil (last ()))
  (test= 'c (last '(a b c))))

(define-test join ()
  (test= '(1 2 3) (join '(1 2) '(3)))
  (test= '(1 2) (join () '(1 2)))
  (test= () (join () ()))
  (test= () (join nil nil))
  (test= () (join nil ()))
  (test= () (join))
  (test= () (join ()))
  (test= '(1) (join '(1) nil))
  (test= '(a) (join '(a) ()))
  (test= '(a) (join nil '(a)))
  (test= '(a) (join '(a))))

(define-test reverse ()
  (test= () (reverse ()))
  (test= '(3 2 1) (reverse '(1 2 3)))
  (test= '(3 2 1 :a) (reverse '(1 2 3 :a))))

(define-test map ()
  (test= () (map (fn (x) x) ()))
  (test= '(1) (map (fn (x) x) '(1)))
  (test= '(2 3 4) (map (fn (x) (+ x 1)) '(1 2 3)))
  (test= '(2 3 4 a: 5) (map (fn (x) (+ x 1)) (list 1 2 3 a: 4)))
  (test= '(:a) (map (fn (x) x) '(:a)))
  (test= '(b: false) (map (fn (x) x) '(b: false)))
  (let (t (map (fn (x) x) '(:a b: false)))
    (test= true t.a)
    (test= false t.b)))

(define-test extend ()
  (test= () (extend ()))
  (test= '(:a) (extend () :a))
  (test= 2 (get (extend '(a: 1) b: 2) 'b))
  (test= 1 (get (extend '(a: 1) b: 2) 'a))
  (test= '(1 :a) (extend '(1) :a)))

(define-test exclude ()
  (test= () (exclude ()))
  (test= () (exclude () :a))
  (test= () (exclude '(:a) :a))
  (test= '(:b) (exclude '(:a :b) :a))
  (test= '(1 2 :b) (exclude '(1 2 :b :c) :c)))

(define-test map-splice ()
  (test= '(1 a 2 a) (map (fn (x) (splice (list x 'a))) '(1 2)))
  (let (f (fn (x) (splice x)))
    (test= () (map f '(())))
    (test= '(1 2) (map f '((1) (2))))
    (test= '(1 2) (map f '((1) () (2))))
    (test= '(1 2) (map f (list nil '(1) nil () '(2))))))

(define-test sub ()
  (test= () (sub ()))
  (test= '(a) (sub '(a)))
  (test= '(b c) (sub '(a b c) 1))
  (test= '(b c) (sub '(a b c d) 1 3))
  (test= "uux" (sub "quux" 1))
  (test= "uu" (sub "quux" 1 3))
  (test= "" (sub "quux" 5))
  (test= '(2 :a) (sub '(1 2 :a) 1))
  (test= '(:a b: 2) (sub '(:a b: 2)))
  (let (t '(1 2 3))
    (test= () (sub t (length t))))
  (let (t '(1 2 3 :a))
    (test= '(:a) (sub t (length t)))))

(define-test search ()
  (test= nil (search "" "a"))
  (test= 0 (search "" ""))
  (test= 0 (search "a" ""))
  (test= 0 (search "abc" "a"))
  (test= 2 (search "abcd" "cd"))
  (test= nil (search "abcd" "ce"))
  (test= nil (search "abc" "z")))

(define-test split ()
  (test= () (split "" ""))
  (test= () (split "" ","))
  (test= (list "a") (split "a" ","))
  (test= (list "a" "") (split "a," ","))
  (test= (list "a" "b") (split "a,b" ","))
  (test= (list "a" "b" "") (split "a,b," ",")))

(define-test reduce ()
  (test= 'a (reduce (fn (a b) (+ a b)) '(a)))
  (test= 6 (reduce (fn (a b) (+ a b)) '(1 2 3)))
  (test= '(1 (2 3))
         (reduce
          (fn (a b) (list a b))
          '(1 2 3)))
  (test= '(1 2 3 4 5)
         (reduce
          (fn (a b) (join a b))
          '((1) (2 3) (4 5)))))

(define-test keep ()
  (test= () (keep (fn (x) x) ()))
  (let (even (fn (x) (= (% x 2) 0)))
    (test= '(6) (keep even '(5 6 7))))
  (test= '((1) (2 3)) (keep some? '(() (1) () (2 3)))))

(define-test find ()
  (test= nil (find (fn (x) x) ()))
  (test= 7 (find (fn (x) x) '(7)))
  (test= true (find (fn (x) (= x 7)) '(2 4 7))))

(define-test flat ()
  (test= 7 (flat 7))
  (test= 7 (flat1 7))
  (test= '(a b) (flat '(a b)))
  (test= '(a b) (flat1 '(a b)))
  (test= '(a b) (flat '(a (b))))
  (test= '(a b) (flat1 '(a (b))))
  (test= '(a b c) (flat '(a ((b) c))))
  (test= '(a (b) c) (flat1 '(a ((b) c)))))

(define-test sort ()
  (test= '(a b c) (sort '(c a b)))
  (test= '(3 2 1) (sort '(1 2 3) >)))

(define-test type ()
  (test= true (string? "abc"))
  (test= false (string? 17))
  (test= false (string? '(a)))
  (test= false (string? true))
  (test= false (string? (table)))
  (test= false (number? "abc"))
  (test= true (number? 17))
  (test= false (number? '(a)))
  (test= false (number? true))
  (test= false (number? (table)))
  (test= false (boolean? "abc"))
  (test= false (boolean? 17))
  (test= false (boolean? '(a)))
  (test= true (boolean? true))
  (test= false (boolean? (table)))
  (test= false (function? 17))
  (test= false (function? "foo"))
  (test= false (function? '(a)))
  (test= false (function? (table)))
  (test= true (function? (fn () 17)))
  (test= false (list? "abc"))
  (test= false (list? 17))
  (test= true (list? '(a)))
  (test= false (list? true))
  (test= false (list? (table)))
  (test= false (table? "abc"))
  (test= false (table? 17))
  (test= false (table? '(a)))
  (test= false (table? true))
  (test= true (table? (table))))

(define-test apply ()
  (test= 4 (apply (fn (a b) (+ a b)) '(2 2)))
  (test= '(2 2) (apply (fn a a) '(2 2)))
  (let (t '(1))
    (set (get t 'foo) 17)
    (test= 17 (apply (fn a (get a 'foo)) t)))
  (test= 42 (apply (fn (:foo) foo) (list foo: 42)))
  (test= 42 (apply (fn ((:foo)) foo) (list (list foo: 42)))))

(define-test eval ()
  (test= 4 (eval '(+ 2 2)))
  (test= 5 (eval '(let (a 3) (+ 2 a))))
  (test= 9 (eval '(do (define x 7) (+ x 2))))
  (test= 6 (eval '(apply + '(1 2 3)))))

(define-test parameters ()
  (test= 42 ((fn ((a)) a) '(42)))
  (let (f (fn (a (b c)) (list a b c)))
    (test= '(1 2 3) (f 1 '(2 3))))
  (let (f (fn (a (b rest: c) rest: d) (list a b c d)))
    (test= '(1 2 (3 4) (5 6 7)) (f 1 '(2 3 4) 5 6 7)))
  (test= '(3 4) ((fn (a b rest: c) c) 1 2 3 4))
  (let (f (fn (w (x rest: y) rest: z) (list y z)))
    (test= '((3 4) (5 6 7)) (f 1 '(2 3 4) 5 6 7)))
  (test= 42 ((fn (:foo) foo) foo: 42))
  (test= 42 ((fn ((:foo)) foo) (list foo: 42)))
  (let (f (fn (a bar: b (:foo)) (list a b foo)))
    (test= '(10 20 42) (f 10 bar: 20 (list foo: 42)))))

(define-test module ()
  (test= "foo" (module-key 'foo))
  (test= "foo/bar" (module-key '(foo bar)))
  (test= "foo/bar/baz" (module-key '(foo (bar baz)))))

(define-test utilities ()
  (test= '(a 3 b 2 c 1)
         (mapo (fn (_ x) x) '(table b: 2 c: 1 a: 3))))
